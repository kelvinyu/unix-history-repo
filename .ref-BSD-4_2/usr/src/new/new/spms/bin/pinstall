#! /bin/csh -f
#
# Install SPMS Software Project Management System
#

if (!(-d /usr/new)) then
	echo "/usr/new: No such directory"
	exit(1)
endif
if (!(-d /usr/new/lib)) then
	echo "/usr/new/lib: No such directory"
	exit(1)
endif

# pexec relies upon the PROJECT environment variable
# which MUST be set to the root directory of the project.
cd ..
setenv PROJECT $cwd

set PATH = (/usr/new /usr/ucb /bin /usr/bin)

#install libraries
foreach p (libhash libpdb libslist libspms libtree)
	cd $PROJECT/src/lib/$p/src
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make install
end

#install programs in /usr/new
cd $PROJECT/src/bin
foreach p (*)
	cd $PROJECT/src/bin/$p
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make install
end
rehash

# reconstruct Makefile dependencies and update
pexec -Tupdate "mkmf; make update"

# clean up
pexec -Tclean "make clean"

#install manual pages in /usr/man/mann
cp $PROJECT/man/mann/*.n /usr/man/mann

# create ~root^ and ~root^usr projects
cd /
mkproject -d -Tproject ~root^
cd /usr/src
mkproject -Nusr -Tproject ~root^.../usr/src > /dev/null << 'EOF'
/usr source
'EOF'

# connect spms project to ~root^usr
mkproject -d ~root^usr/new/spms

exit(0)
